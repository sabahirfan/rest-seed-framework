plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version '6.0.1.5171'
    id 'checkstyle'
}

group = 'com.dtg.alpha'
version = '1.0.0'
archivesBaseName = 'rest-seed-framework'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Core dependencies
    implementation 'io.rest-assured:rest-assured:5.4.1'
    implementation 'io.rest-assured:json-path:5.4.1'
    implementation 'io.rest-assured:xml-path:5.4.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    
    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    
    // Lombok for reducing boilerplate
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/OWNER/REPOSITORY")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

checkstyle {
    toolVersion = '10.14.0'
    configFile = file("config/checkstyle/checkstyle.xml")
}